apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'realm-android'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            //keystore的文件路径，可以使绝对路径也可以是相对路径
            storeFile file(RELEASE_STORE_FILE)
            //keystore的密码
            storePassword RELEASE_STORE_PASSWORD
            //keystore中key的别名
            keyAlias RELEASE_KEY_ALIAS
            //keystore中key的密码
            keyPassword RELEASE_KEY_PASSWORD
            //两种签名方式同时启用
            v1SigningEnabled true
            v2SigningEnabled true
        }
        /**
         * 设置你的keystore相关
         * demo中只是一个示例，使用时请根据实际情况进行配置
         */
        release {
            //keystore的文件路径，可以使绝对路径也可以是相对路径
            storeFile file(RELEASE_STORE_FILE)
            //keystore的密码
            storePassword RELEASE_STORE_PASSWORD
            //keystore中key的别名
            keyAlias RELEASE_KEY_ALIAS
            //keystore中key的密码
            keyPassword RELEASE_KEY_PASSWORD
            //两种签名方式同时启用
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "String", "APP_NAME", '"XPlan"'
            versionNameSuffix "-debug-" + getDate()
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            //signingConfig signingConfigs.debug
            jniDebuggable true
            debuggable true
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "String", "APP_NAME", '"XPlan"'
            minifyEnabled true//混淆
            zipAlignEnabled true//Zipalign优化
            shrinkResources true// 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release //签名
            jniDebuggable false
            debuggable false
        }
    }

    /**
     * 设置不同渠道打包通道
     */
    productFlavors {
        Offcial {}
        Google {}
        Xiaomi{}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "xplan_${variant.versionName}_${variant.versionCode}_${variant.flavorName}_${getDate()}.apk".toLowerCase()
        }
    }
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.umeng.sdk:common:latest.integration'
    implementation 'com.umeng.sdk:analytics:latest.integration'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.google.code.gson:gson:2.8.2'
}
